plugins {
	id "java"
	id "idea"
	id "org.springframework.boot" version "2.2.2.RELEASE"
	id "io.spring.dependency-management" version "1.0.8.RELEASE"
	id "com.bmuschko.docker-remote-api" version "6.1.1"
}

group = 'io.maslick'
version = '0.1'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.hazelcast:hazelcast:3.12.5'
	implementation 'com.hazelcast:hazelcast-spring:3.12.5'
	implementation 'com.hazelcast:hazelcast-kubernetes:2.0'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
task createDockerfile(type: Dockerfile) {
	destFile = project.file('build/libs/Dockerfile')
	from 'maslick/minimalka'
	instruction 'LABEL maintainer="pavel.masloff@gmail.com"'
	exposePort 8080
	copyFile jar.archiveFileName.get(), '/app/app.jar'
	workingDir '/app'
	instruction 'CMD java $HZ_ARGS -Dk8s=$K8S -jar app.jar'
}

check.dependsOn createDockerfile

task dockerBuild(type: Exec) {
	commandLine 'docker', 'build', '-t', "maslick/hazzelcuster", 'build/libs'
	dependsOn build
	test.enabled = false
}

bootRun {
	systemProperties = System.properties
}
